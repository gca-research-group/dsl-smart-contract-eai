grammar br.edu.unijui.gca.smartce.SmartCE with org.eclipse.xtext.common.Terminals

import "http://www.edu.br/unijui/gca/smartce/SmartCE"

Model: 
	{Model}
	imports+=Import*
	contracts+=Contract*;

Import:
     'import' importedNamespace=QualifiedNameWithWildcard
;

Contract: 
	'Contract' name=ID '{'
		'beginDate' '=' beginDate=STRING
		'dueDate' '=' dueDate=STRING
		'application' '=' application=Application
		'process' '=' process=Process
		clauses+=Clause*
		actions+=Action*
	'}'; 

Clause: 
	('Right' | 'Prohibition' | 'Obligation') name=ID '{'
		'description' '=' description=STRING
		'rolePlayer' '=' rolePlayer=[Party]
		'operation' '=' operation=Operation
		'condition' '{' 
			condition=Condition  
		'}'
		'onBreach' onBreach=OnBreach ('onSuccess' onSuccess=OnSuccess)?
	 '}' 
;

Condition: 
	BusinessRule | CompositeCondition
;

BusinessRule:
	Timeout | OperationLimit | BusinessDay |TimeInterval | MessageContent	
;

CompositeCondition:
	conditions+=BusinessRule logicalOperators+=LogicalOperator conditions+=BusinessRule (logicalOperators+=LogicalOperator conditions+=BusinessRule)*
;

LogicalOperator:
	name=('AND' | 'OR' | 'NOT')
;

Timeout:
	'Timeout' '(' value=INT ')'
;

OperationLimit:
	'OperationLimit' '(' operationsNumber=INT 'by' timeUnit=TimeUnit  ')'
;

BusinessDay:
	('BusinessDay' '(' start=WeekDay 'to' end=WeekDay ')')
	
;
	
enum Operation:
    PUSH='push' | POLL='poll' | WRITE='write' | READ = 'read' | REQUEST='request' | RESPONSE='response' 
;

enum WeekDay:
    SUNDAY='Sunday' | MONDAY='Monday' | TUESDAY='Tuesday' | WEDNESDAY = 'Wednesday' | THURSDAY='Thursday' | FRIDAY='Friday' | SATURDAY='Saturday'
;
    
enum TimeUnit:
    SECOND='Second' |  MINUTE='Minute' | HOUR = 'Hour' |  DAY='Day' | WEEK='Week' | MONTH='Month' | YEAR='Year'
;
      
TimeInterval: 
	'TimeInterval' '(' start=STRING 'to' end=STRING')'
; 

MessageContent:
	'MessageContent' '(' content=STRING ')' | /* Add an user help -> This field requires an xpath expression */	
	'MessageContent' '(' content=STRING 'by' timeUnit=TimeUnit')' 
;

Party: 
	Application | Process
;

Application: 
	name=ID':' description=STRING
;

Process: 
	name=ID':' description=STRING  
;

OnSuccess:
	'(' action=[Action] ',' message=Expression  ')' 
;

OnBreach:
	'(' action=[Action] ',' message=Expression  ')' 
;

Action:
	BusinessAction | EventLog;

BusinessAction:
	'BusinessAction' name=ID '(' parameters+=Variable (',' parameters+=Variable)* ')'
;

EventLog:
	'EventLog' name=ID	'(' parameters+=Variable (',' parameters+=Variable)* ')'
;

Variable:
	name=ID ':' type=ID
;

Expression:
	Negation ({BinaryOperator.left=current} (symbol='&&' | symbol="||") right=Negation)*;

Negation returns Expression:
	Comparison | {UnaryOperator} symbol='!' expression=Comparison;

Comparison returns Expression:
	Plus ({BinaryOperator.left=current} (symbol='<=' | symbol='>=' | symbol='>' | symbol='<' | symbol='!='
	| symbol='==' | symbol='is' | symbol='as') right=Plus)*;

Plus returns Expression:
	Factor ({BinaryOperator.left=current} ('+' | '-') right=Factor)*;

Factor returns Expression:
	Negative ({BinaryOperator.left=current} ('*' | '/') right=Negative)*;

Negative returns Expression:
	Primary | {UnaryOperator} '-' expression=Primary;

Primary returns Expression:
	LiteralValue | '(' Expression ')';
	
LiteralValue:
	NumericValue | StringValue | VariableValue | FunctionCall;

NumericValue:
	value=INT;

StringValue:
	value=STRING;

VariableValue:
	value=QualifiedName;

FunctionCall:
	name=QualifiedName '(' params+=Expression (',' params+=Expression)* ')'
;

QualifiedName:
	ID ('.' ID)*;
	
QualifiedNameWithWildcard:
	QualifiedName '.*'; 

DATE: YEAR'-'MONTH'-'DAY'T'HOUR':'MIN':'SEC;
YEAR: INT;
MONTH: INT;
DAY: INT;
HOUR: INT;
MIN: INT;
SEC: INT;



